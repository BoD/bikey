// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

ext.globalProjectName = 'bikey'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://JRAF.org/static/maven/2'
        }
    }
}


def gitSha1() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildDate() {
    def dateFormat = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm\'Z\'')
    dateFormat.setTimeZone(TimeZone.getTimeZone('UTC'))
    return dateFormat.format(new Date())
}

def filterResources(buildVariant) {
    // Copy and 'filter' (replace {} placeholders) the resources
    def path = buildVariant.outputs[0].processResources.resDir.getPath()

    // We can't use the project.properties object directly in the filter, because
    // it's a HashMap, and we need a Properties object.
    // We can't just do buildProperties.putAll(project.properties) because this
    // crashes if a property exists with a "null" value.
    Properties buildProperties = new Properties();
    Set keys = project.properties.keySet();
    for(Object key : keys) {
        Object value = project.properties.get(key);
        if(value != null)
            buildProperties .put(key, value);
    }
    copy {
        from path + '/values/values.xml'
        filter(ReplaceTokens, tokens: buildProperties, beginToken: '{', endToken: '}')
        into path + '/values'
        rename 'values.xml', 'values.xml.expanded'
    }
    // Override the file
    copy {
        from path + '/values/values.xml.expanded'
        into path + '/values'
        rename 'values.xml.expanded', 'values.xml'
    }
    // Delete the temporary file
    file(path + '/values/values.xml.expanded').delete()
}

ext.buildNumberFile = file('build.number')
ext.buildNumberProperties = new Properties()
buildNumberProperties.load(new FileInputStream(buildNumberFile))
ext.buildNumber = buildNumberProperties['build.number'].toInteger()
